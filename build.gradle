import org.gradle.api.tasks.testing.logging.TestExceptionFormat
import org.gradle.api.tasks.testing.logging.TestLogEvent
import org.gradle.jvm.toolchain.JavaLanguageVersion
import org.gradle.jvm.toolchain.JavaToolchainService
import org.gradle.jvm.toolchain.JvmImplementation
import org.gradle.api.artifacts.VersionCatalogsExtension

ext.determineJavaLanguageVersion = { Project project, int requestedVersion ->
    JavaToolchainService toolchainService = project.extensions.getByType(JavaToolchainService)
    JavaLanguageVersion requested = JavaLanguageVersion.of(requestedVersion)
    int runtimeVersion = Runtime.version().feature()
    JavaLanguageVersion fallback = JavaLanguageVersion.of(runtimeVersion)

    if (requested.asInt() == fallback.asInt()) {
        return requested
    }

    try {
        toolchainService.launcherFor {
            languageVersion.set(requested)
            implementation.set(JvmImplementation.VENDOR_SPECIFIC)
        }.get()
        return requested
    } catch (Exception ignored) {
        project.logger.lifecycle(
                "Java {} toolchain not found; falling back to runtime Java {} for project '{}'.",
                requested.asInt(),
                fallback.asInt(),
                project.name
        )
        return fallback
    }
}

allprojects {
    group = providers.gradleProperty('group').orElse('com.geewit.utils').get()
    version = providers.gradleProperty('version').orElse('1.0.0').get()
}

subprojects { subproject ->
    def libs = extensions.getByType(VersionCatalogsExtension).named('libs')
    def configuredJavaVersion = providers.gradleProperty('io.geewit.java.version')
            .map(Integer.&parseInt)
            .orElse(Runtime.version().feature())
            .get()
    def javaLanguageVersion = determineJavaLanguageVersion(subproject, configuredJavaVersion) as JavaLanguageVersion
    def publishEnabled = providers.gradleProperty('io.geewit.publish.enabled').map { it.toBoolean() }.orElse(false).get()

    apply plugin: 'java-library'
    if (publishEnabled) {
        apply plugin: 'maven-publish'
        apply plugin: 'signing'
    }

    java {
        toolchain {
            languageVersion.set(javaLanguageVersion)
            implementation.set(JvmImplementation.VENDOR_SPECIFIC)
        }
        withSourcesJar()
        withJavadocJar()
    }

    configurations {
        configureEach { config ->
            config.exclude([group: 'com.google.protobuf'])
            config.exclude([group: 'org.springframework.boot', module: 'spring-boot-dependencies'])
            config.exclude([group: 'org.springframework.boot', module: 'spring-boot-starter-web'])
            config.exclude([group: 'org.springframework', module: 'spring-webmvc'])
            config.exclude([group: 'org.slf4j', module: 'slf4j-log4j12'])
            config.exclude([group: 'org.slf4j', module: 'slf4j-simple'])
            config.exclude([group: 'org.yaml'])
            config.exclude([group: 'org.mockito', module: 'mockito-all'])
            config.exclude([group: 'org.apache.ant'])
            config.exclude([group: 'io.netty', module: 'netty-tcnative-classes'])
            config.exclude([group: 'ua.net.nlp', module: 'morfologik-ukrainian-search'])

            resolutionStrategy {
                eachDependency { details ->
                    if (libs.versions != null) {
                        if (details.requested.group as String == 'commons-codec' && details.requested.name as String == 'commons-codec') {
                            if (libs.versions.find('commons-codec').present) {
                                details.useVersion libs.versions.find('commons-codec').get().requiredVersion
                            }
                        } else if (details.requested.group as String == 'commons-io' && details.requested.name as String == 'commons-io') {
                            if (libs.versions.find('commons-io').present) {
                                details.useVersion libs.versions.find('commons-io').get().requiredVersion
                            }
                        } else if (details.requested.group as String == 'org.apache.commons' && details.requested.name as String == 'commons-lang3') {
                            if (libs.versions.find('commons-lang3').present) {
                                details.useVersion libs.versions.find('commons-lang3').get().requiredVersion
                            }
                        } else if (details.requested.group as String == 'org.slf4j' && details.requested.name as String == 'slf4j-api') {
                            if (libs.versions.find('slf4j').present) {
                                details.useVersion libs.versions.find('slf4j').get().requiredVersion
                            }
                        } else if (details.requested.group as String == 'org.projectlombok') {
                            if (libs.versions.find('lombok').present) {
                                details.useVersion libs.versions.find('lombok').get().requiredVersion
                            }
                        } else if ((details.requested.group as String).startsWith('com.fasterxml.jackson')) {
                            if (libs.versions.find('jackson2.core').present) {
                                details.useVersion libs.versions.find('jackson2.core').get().requiredVersion
                            }
                        } else if ((details.requested.group as String).startsWith('org.springframework.boot')) {
                            if (libs.versions.find('spring-boot').present) {
                                details.useVersion libs.versions.find('spring-boot').get().requiredVersion
                            }
                        } else if (details.requested.group as String == 'org.springframework.data') {
                            if (libs.versions.find('spring.data').present) {
                                details.useVersion libs.versions.find('spring.data').get().requiredVersion
                            }
                        } else if ((details.requested.group as String).startsWith('org.springframework')) {
                            if (libs.versions.find('spring').present) {
                                details.useVersion libs.versions.find('spring').get().requiredVersion
                            }
                        } else if (details.requested.group as String == 'org.junit.jupiter') {
                            if (libs.versions.find('junit-jupiter').present) {
                                details.useVersion libs.versions.find('junit-jupiter').get().requiredVersion
                            }
                        } else if (details.requested.group as String == 'org.junit.platform') {
                            if (libs.versions.find('junit-platform').present) {
                                details.useVersion libs.versions.find('junit-platform').get().requiredVersion
                            }
                        } else if (details.requested.group as String == 'org.apache.logging.log4j') {
                            if (libs.versions.find('log4j').present) {
                                details.useVersion libs.versions.find('log4j').get().requiredVersion
                            }
                        }
                    }
                }
            }
        }
    }

    tasks.withType(JavaCompile).configureEach {
        options.encoding = 'UTF-8'
    }

    tasks.withType(Test).configureEach {
        useJUnitPlatform()
        testLogging {
            events TestLogEvent.FAILED, TestLogEvent.SKIPPED, TestLogEvent.PASSED
            exceptionFormat = TestExceptionFormat.FULL
        }
    }

    if (publishEnabled) {
        publishing {
        publications {
            create('mavenJava', MavenPublication) {
                from components.java
                pom {
                    name = providers.gradleProperty('POM_NAME').orElse(project.name).get()
                    description = providers.gradleProperty('POM_DESCRIPTION').orElse(project.name).get()
                    url = providers.gradleProperty('POM_URL').orElse('').get()
                    licenses {
                        license {
                            name = providers.gradleProperty('POM_LICENCE_NAME').orElse('MIT License').get()
                            url = providers.gradleProperty('POM_LICENCE_URL').orElse('https://mit-license.org/').get()
                        }
                    }
                    developers {
                        developer {
                            id = providers.gradleProperty('POM_DEVELOPER_ID').orElse('').get()
                            name = providers.gradleProperty('POM_DEVELOPER_NAME').orElse('').get()
                            email = providers.gradleProperty('POM_DEVELOPER_EMAIL').orElse('').get()
                        }
                    }
                    scm {
                        url = providers.gradleProperty('POM_SCM_URL').orElse('').get()
                        connection = providers.gradleProperty('POM_SCM_CONNECTION').orElse('').get()
                        developerConnection = providers.gradleProperty('POM_SCM_DEV_CONNECTION').orElse('').get()
                    }
                }
            }
        }
        repositories {
            mavenLocal()
            maven {
                name = 'nexus'
                def releaseUrl = providers.gradleProperty('nexus.releaseUrl').orElse('').get()
                def snapshotUrl = providers.gradleProperty('nexus.snapshotUrl').orElse('').get()
                def target = project.version.toString().endsWith('-SNAPSHOT') && snapshotUrl ? snapshotUrl : releaseUrl
                if (!target) {
                    logger.lifecycle('No nexus repository url configured; skipping remote publication for {}', project.name)
                }
                def fallbackRepo = layout.buildDirectory.dir("publications/${project.name}").get().asFile.toURI()
                url = target ? uri(target) : fallbackRepo
                credentials {
                    username = providers.gradleProperty('nexus.username').orElse(System.getenv('NEXUS_USERNAME') ?: '').get()
                    password = providers.gradleProperty('nexus.password').orElse(System.getenv('NEXUS_PASSWORD') ?: '').get()
                }
            }
        }
        }

        signing {
            required { providers.gradleProperty('io.geewit.signing.enabled').map { it.toBoolean() }.orElse(false).get() && !project.version.toString().endsWith('SNAPSHOT') }
            sign publishing.publications.mavenJava
        }
    }
}
