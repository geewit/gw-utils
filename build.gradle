import org.gradle.api.tasks.testing.logging.TestExceptionFormat
import org.gradle.api.tasks.testing.logging.TestLogEvent
import org.jreleaser.model.Active

import java.nio.charset.StandardCharsets

plugins {
    // 版本号仍然通过 settings.gradle 里的 pluginManagement 解析
    id 'org.jreleaser' apply false
}

// 动态选择 Java 语言版本：优先使用 io.geewit.java.version，找不到对应 toolchain 就回退到当前运行时版本
ext.determineJavaLanguageVersion = { Project project, int requestedVersion ->
    // 这里要求项目已经应用了 java / java-library 之类的插件
    JavaToolchainService toolchainService = project.extensions.getByType(JavaToolchainService)
    JavaLanguageVersion requested = JavaLanguageVersion.of(requestedVersion)
    int runtimeVersion = Runtime.version().feature()
    JavaLanguageVersion fallback = JavaLanguageVersion.of(runtimeVersion)

    if (requested.asInt() == fallback.asInt()) {
        return requested
    }

    try {
        toolchainService.launcherFor {
            languageVersion.set(requested)
            implementation.set(JvmImplementation.VENDOR_SPECIFIC)
        }.get()
        return requested
    } catch (Exception ignored) {
        project.logger.lifecycle(
                "Java {} toolchain not found; falling back to runtime Java {} for project '{}'.",
                requested.asInt(),
                fallback.asInt(),
                project.name
        )
        return fallback
    }
}

// -------------------------------
// 外部开关 & 凭据（懒加载友好写法）
// -------------------------------
def publishEnabled = providers.gradleProperty('io.geewit.publish.enabled')
        .map { it.toBoolean() }
        .getOrElse(false)

def gpgKeyId = providers.gradleProperty('signing.keyId').getOrNull()
def gpgKeyPassword = providers.gradleProperty('signing.password').getOrNull()
def gpgSecretKeyRingDir = providers.gradleProperty('signing.secretKeyRingDir').getOrNull()
def gpgSecretKeyRingFilename = providers.gradleProperty('signing.secretKeyRingFilename').getOrNull()

def sonatypeUsername = providers.gradleProperty('SONATYPE_USERNAME').getOrNull()
def sonatypePassword = providers.gradleProperty('SONATYPE_PASSWORD').getOrNull()

// 如有其它地方需要，也还能通过 ext 访问
ext.publishEnabled = publishEnabled
ext.sonatypeUsername = sonatypeUsername
ext.sonatypePassword = sonatypePassword


allprojects {
    version = providers.gradleProperty('version').getOrElse('1.0.0')
}

subprojects { subproject ->

    Properties localProps = new Properties()
    File localPropsFile = subproject.file('gradle.properties')
    if (localPropsFile.exists()) {
        localPropsFile.withInputStream { localProps.load(it) }
    }

    String localGroup = localProps.getProperty('group')
    String localArtifactId = localProps.getProperty('artifactId')
    // 只使用子项目自己的 group / artifactId；没配就用默认值
    subproject.group = localGroup ?: 'com.geewit.utils'
    // ------------------------------------------------
    // 1. 先应用插件，让 JavaToolchainService 等扩展可用
    // ------------------------------------------------
    subproject.pluginManager.apply('java-library')

    if (publishEnabled) {
        subproject.pluginManager.apply('org.jreleaser')
    }

    // ------------------------------------------------
    // 2. 尝试从 version catalog 中获取 libs（可能为 null）
    // ------------------------------------------------
    VersionCatalog libs = null
    try {
        // version catalog `libs` 会以 extension 形式挂在 dependencies.extensions 下
        libs = subproject.dependencies.extensions.findByName('libs') as VersionCatalog
    } catch (Exception ignored) {
        // 没拿到就算了，后面 resolutionStrategy 会自动跳过
    }

    // ------------------------------------------------
    // 3. 初始化 Java toolchain（此时已应用 java-library 插件）
    // ------------------------------------------------
    def configuredJavaVersion = providers.gradleProperty('io.geewit.java.version')
            .map(Integer.&parseInt)
            .orElse(Runtime.version().feature())
            .get()
    def javaLanguageVersion = determineJavaLanguageVersion(subproject, configuredJavaVersion) as JavaLanguageVersion

    subproject.java {
        toolchain {
            languageVersion.set(javaLanguageVersion)
            implementation.set(JvmImplementation.VENDOR_SPECIFIC)
        }
        withSourcesJar()
        withJavadocJar()
    }

    // ------------------------------------------------
    // 4. 统一依赖排除 + 版本对齐
    // ------------------------------------------------
    subproject.configurations {
        configureEach { config ->
            config.exclude([group: 'com.google.protobuf'])
            config.exclude([group: 'org.springframework.boot', module: 'spring-boot-dependencies'])
            config.exclude([group: 'org.springframework.boot', module: 'spring-boot-starter-web'])
            config.exclude([group: 'org.springframework', module: 'spring-webmvc'])
            config.exclude([group: 'org.slf4j', module: 'slf4j-log4j12'])
            config.exclude([group: 'org.slf4j', module: 'slf4j-simple'])
            config.exclude([group: 'org.yaml'])
            config.exclude([group: 'org.mockito', module: 'mockito-all'])
            config.exclude([group: 'org.apache.ant'])
            config.exclude([group: 'io.netty', module: 'netty-tcnative-classes'])
            config.exclude([group: 'ua.net.nlp', module: 'morfologik-ukrainian-search'])

            resolutionStrategy {
                if (!libs) {
                    // 没有 version catalog 就直接跳过
                    return
                }
                eachDependency { details ->

                    def requestedGroup = details.requested.group as String
                    def requestedName = details.requested.name as String

                    // 小工具：按 alias 从 libs 里取版本并应用
                    def useCatalogVersion = { String alias ->
                        def v = libs.findVersion(alias)
                        if (v.present) {
                            details.useVersion v.get().requiredVersion
                        }
                    }

                    if (requestedGroup == 'commons-codec' && requestedName == 'commons-codec') {
                        useCatalogVersion('commons-codec')
                    } else if (requestedGroup == 'commons-io' && requestedName == 'commons-io') {
                        useCatalogVersion('commons-io')
                    } else if (requestedGroup == 'org.apache.commons' && requestedName == 'commons-lang3') {
                        useCatalogVersion('commons-lang3')
                    } else if (requestedGroup == 'org.slf4j' && requestedName == 'slf4j-api') {
                        useCatalogVersion('slf4j')
                    } else if (requestedGroup == 'org.projectlombok') {
                        useCatalogVersion('lombok')
                    } else if (requestedGroup == 'com.fasterxml.jackson.core' && requestedName == 'jackson-annoations') {
                        useCatalogVersion('jackson.annoations')
                    } else if (requestedGroup.startsWith('tools.jackson')) {
                        useCatalogVersion('jackson.core')
                    } else if (requestedGroup.startsWith('org.springframework.boot')) {
                        useCatalogVersion('spring-boot')
                    } else if (requestedGroup == 'org.springframework.data') {
                        useCatalogVersion('spring.data')
                    } else if (requestedGroup.startsWith('org.springframework')) {
                        useCatalogVersion('spring')
                    } else if (requestedGroup == 'org.junit.jupiter') {
                        useCatalogVersion('junit-jupiter')
                    } else if (requestedGroup == 'org.junit.platform') {
                        useCatalogVersion('junit-platform')
                    } else if (requestedGroup == 'org.apache.logging.log4j') {
                        useCatalogVersion('log4j')
                    }
                }
            }
        }
    }

    // ------------------------------------------------
    // 5. 编译 & 测试配置
    // ------------------------------------------------
    tasks.withType(JavaCompile).configureEach {
        options.encoding = StandardCharsets.UTF_8.name()
    }

    tasks.withType(Javadoc).configureEach {
        options.encoding = StandardCharsets.UTF_8.name()
        options.charSet = StandardCharsets.UTF_8.name()
        options.docEncoding = StandardCharsets.UTF_8.name()
        options.addBooleanOption('Xdoclint:none', true)
        options.addBooleanOption('quiet', true)
    }

    tasks.withType(Test).configureEach {
        useJUnitPlatform()
        testLogging {
            events TestLogEvent.FAILED, TestLogEvent.SKIPPED, TestLogEvent.PASSED
            exceptionFormat = TestExceptionFormat.FULL
        }
    }

    // ------------------------------------------------
    // 6. 发布配置（只在 io.geewit.publish.enabled = true 时启用）
    // ------------------------------------------------
    if (publishEnabled) {
        subproject.jreleaser {
            announce {
                active = Active.NEVER
            }
            assemble {
                active = Active.NEVER
            }
            catalog {
                active = Active.NEVER
            }
            project {
                name = localArtifactId ?: subproject.name
                description = 'Geewit utility libraries'
                authors = ['Huang Fang']
                license = 'MIT'
                vendor = 'Geewit inc.'
            }
            signing {
                // Maven Central 要求签名
                active = Active.ALWAYS
                armored = true
                mode = 'COMMAND'
                command {
                    // The executable used for signing.
                    // Defaults to `gpg[.exe]`.
                    // When mode = `COMMAND`.
                    //
                    executable = 'gpg'

                    // The directory from which gpg will load keyrings.
                    // Defaults to empty.
                    // When mode = `COMMAND`.
                    //
                    homeDir = gpgSecretKeyRingDir

                    // The "name" of the key to sign with.
                    // Defaults to empty.
                    // When mode = `COMMAND`.
                    keyName = ''

                    // The path to a public keyring to add to the list of keyrings.
                    // Defaults to empty.
                    // When mode = `COMMAND`.
                    //
                    publicKeyring = gpgSecretKeyRingFilename

                    // Whether to add the default keyrings from gpg's home directory to the list of used keyrings.
                    // Defaults to `true`.
                    // When mode = `COMMAND`.
                    //
                    defaultKeyring = true

                    // Sets the arguments to be passed to gpg.
                    // When mode = `COMMAND`.
                    //
                    args = ['--no-random-seed-file']
                }
            }
            deploy {
                maven {
                    //
                    mavenCentral {
                        // Deployers require a name.
                        //
                        app {

                            // Enables or disables the deployer.
                            // Supported values are [`NEVER`, `ALWAYS`, `RELEASE`, `SNAPSHOT`].
                            // Defaults to `NEVER`.
                            //
                            active = Active.RELEASE

                            // URL where the MavenCentral service is enabled.
                            //
                            url = 'https://central.sonatype.com/api/v1/publisher'

                            // Activates publication of snapshot artifacts.
                            // Defaults to `false`.
                            //
                            snapshotSupported = false

                            // The username required for authorization.
                            //
                            username = sonatypeUsername

                            // Password for login into the MAVENCENTRAL service.
                            //
                            password = sonatypePassword

                            // The authorization method to use.
                            // Supported values are [`NONE`, `BASIC`, `BEARER`].
                            // `Basic` requires both username & password.
                            // `BEARER` requires a token (set as password).
                            // Defaults to `BEARER`.
                            //
                            authorization = 'BEARER'

                            // Signs artifacts with the configured credentials.
                            // The Signing section must be configured as well.
                            // Defaults to `false` unless `applyMavenCentralRules` is set to `true`.
                            //
                            sign = true

                            // Checksums all artifacts with `MD5`, `SHA-1`, `SHA-256`, and `SHA-512`.
                            // Defaults to `false` unless `applyMavenCentralRules` is set to `true`.
                            //
                            checksums = true

                            // Verifies that a matching `-sources.jar` artifact is staged.
                            // Defaults to `false` unless `applyMavenCentralRules` is set to `true`.
                            //
                            sourceJar = true

                            // Verifies that a matching `-javadoc.jar` artifact is staged.
                            // Defaults to `false` unless `applyMavenCentralRules` is set to `true`.
                            //
                            javadocJar = true

                            // Verifies that POM files comply with the minimum requirements for publication
                            // to Maven Central. Checks rules using PomChecker.
                            // Defaults to `false` unless `applyMavenCentralRules` is set to `true`.
                            //
                            verifyPom = true

                            // Verifies pom files, signs all artifacts, verifies that matching `-sources.jar` and
                            // `-javadoc.jar` artifacts are also staged.
                            // Defaults to `false`.
                            //
                            applyMavenCentralRules = true


                            // List of directories where staged artifacts can be found.
                            //
                            stagingRepository(layout.buildDirectory.dir('staging-deploy').get().asFile.absolutePath)

                            // Defines the connection timeout in seconds.
                            // Defaults to `20`.
                            //
                            connectTimeout = 20

                            // Defines the read timeout in seconds.
                            // Defaults to `60`.
                            //
                            readTimeout = 60

                            // URL for checking artifacts may be already deployed.
                            // Additional template tokens: `groupId`, `artifactId`, `version`, `path`, `filename`.
                            //
                            verifyUrl = "https://repo1.maven.org/maven2/{{path}}/{{filename}}"

                            // Registered publication namespace.
                            // Defaults to `${project.java.groupId}`.
                            //
                            namespace = 'io.geewit'

                            // Deployment identifier used for publication.
                            //
                            deploymentId = "${subproject.group}:${subproject.name}:${rootProject.version}"

                            // Time to wait between state transition checks, in seconds.
                            // Defaults to `20`.
                            //
                            retryDelay = 20

                            // Maximum number of attempts to verify state transition.
                            // Defaults to `100`.
                            //
                            maxRetries = 100
                        }
                    }
                }
            }
            upload {
                active = Active.NEVER
            }
        }
    }
}
